class Poll
  lastOptionIndex: 0

  constructor: ->
    # Count how many options exist and start there
    @lastOptionIndex = $('#js-add-option').children().length - 1

    @attachListener(@getLastOption())

  getLastOption: ->
    $("#poll_options_attributes_#{@lastOptionIndex}_text")

  attachListener: ($element) ->
    $element.on 'input', @inputListener

  removeListener: ($element) ->
    $element.off 'input'

  inputListener: (e) =>
    # We only allow a maximum of 8 poll options
    if @lastOptionIndex < 7
      @removeListener $(e.currentTarget)

      # Create the new form components
      @lastOptionIndex++
      @addOption()
      @attachListener(@getLastOption())

  addOption: ->
    # Append the new input
    input =  "<li class='input-add-on'>" +
                "<span class='input-add-on-item'>#{@lastOptionIndex + 1}</span>" +
                "<input class='input-add-on-field' placeholder='Enter poll option' type='text' name='poll[options_attributes][#{@lastOptionIndex}][text]' id='poll_options_attributes_#{@lastOptionIndex}_text'>" +
              "</li>"
    $(input).appendTo("#js-add-option")

createPoll = ->
  window.Poll = new Poll $(this)

$(document).ready(createPoll)
$(document).on('page:load', createPoll)
